/*
 * Copyright (c) 2012 - 2025 Arvato Systems GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import apiw.*
import api.media.*
import t9t.base.types.*
import t9t.base.api.*
import t9t.base.entities.*
import t9t.base.crud.*
import t9t.base.search.*
import t9t.ai.*
import t9t.ai.tools.*
import t9t.ai.mcp.*

/**
 * Core AI request definitions, draft 1
 */
package (com.arvatosystems) t9t.ai.request {
    default private unsigned trim noControlChars;


    /** Standard CRUD and search requests... */
    class AiModuleCfgCrudRequest                    extends CrudModuleCfgRequest    <AiModuleCfgDTO> {}
    class AiModuleCfgSearchRequest                  extends SearchRequest           <AiModuleCfgDTO, FullTrackingWithVersion> {}

    class AiUserStatusCrudRequest                   extends CrudSurrogateKeyRequest <AiUserStatusRef, AiUserStatusDTO, FullTrackingWithVersion> {}
    class AiUserStatusSearchRequest                 extends SearchRequest           <AiUserStatusDTO, FullTrackingWithVersion> {}

    class AiAssistantCrudRequest                    extends CrudSurrogateKeyRequest <AiAssistantRef, AiAssistantDTO, FullTrackingWithVersion> {}
    class AiAssistantSearchRequest                  extends SearchRequest           <AiAssistantDTO, FullTrackingWithVersion> {}
    // class LeanAiAssistantSearchRequest              extends LeanSearchRequest {}
    class AiAssistantLeanSearchRequest              extends LeanSearchRequest {}

    class AiConversationCrudRequest                 extends CrudSurrogateKeyRequest <AiConversationRef, AiConversationDTO, FullTrackingWithVersion> {}
    class AiConversationSearchRequest               extends SearchRequest           <AiConversationDTO, FullTrackingWithVersion> {}

    class AiChatLogSearchRequest                    extends SearchRequest           <AiChatLogDTO, WriteTracking> {}

    class AiPromptCrudRequest                       extends CrudSurrogateKeyRequest <AiPromptRef, AiPromptDTO, FullTrackingWithVersion> {}
    class AiPromptSearchRequest                     extends SearchRequest           <AiPromptDTO, FullTrackingWithVersion> {}

    /** API to submit user input (initial call without parameters to get the greeting). */
    class AiChatRequest extends RequestParameters return AiChatResponse {
        optional (AiAssistantRef...)                aiAssistantRef;
        optional textChunk                          userInput;
        optional (MediaData)                        userUpload;
    }

    /** System response to user questions. */
    class AiChatResponse extends ServiceResponse {
        required (AiAssistantDescription)           aiAssistant;
        required textChunk List<>                   textOutput;
        optional (MediaData)                        mediaOutput;
    }

    /** A no op request, just done to check if we have permissions to establish a connection. */
    class AiGetSseRequest extends RequestParameters return AiGetToolsResponse {}

    /** Request to return the list of available tools. */
    class AiGetToolsRequest extends RequestParameters return AiGetToolsResponse {}

    class AiGetToolsResponse extends ServiceResponse {
        required (AiToolSpecification) required List<>      tools;
    }

    /** Run the specified tool. */
    class AiRunToolRequest extends RequestParameters   return AiRunToolResponse {
        required defaultExternalId                          name;               // ID of the tool
        optional Json                                       parameters;         // parameters in structured form
        optional Unicode(64000) allowControlChars           arguments;          // parameters as string
        optional Boolean                                    structuredResultAsString;  // set this to true if the receiver does not know all possible result objects (gateway use)
    }

    class AiRunToolResponse extends ServiceResponse {
        required (MediaData) List<>                         contents;             // List of contents of type MediaData, may be null
        optional (AbstractAiToolResult...)                  structuredResponse;   // response if it is a data structure
        optional Unicode(64000) allowControlChars           structuredResponseAsString;  // result as a JSON formatted string, if requested
        optional Boolean                                    isError;              // in case of error during tool execution
    }

    /** Request to return the list of available prompts. */
    class AiGetPromptsRequest extends RequestParameters return AiGetPromptsResponse {
        required int                                        offset;             // offset for pagination. If -1 then no pagination, return all
    }

    class AiGetPromptsResponse extends ServiceResponse {
        required (AiPromptDTO) required List<>              prompts;
        optional Integer                                    nextOffset;
    }

    /** Request to get a specific prompt. */
    class AiGetPromptRequest extends RequestParameters return AiGetPromptResponse {
        required defaultExternalId                                     name; // ID of the prompt
        required Unicode(64000) allowControlChars required Map<String> arguments; // key: parameter name, value: parameter value
    }

    class AiGetPromptResponse extends ServiceResponse {
        required Unicode(64000) allowControlChars           prompt;             // the prompt with populated parameters
        required Unicode(4000) allowControlChars            description;        // a slightly longer description of the prompt
    }
}
