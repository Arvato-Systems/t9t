/*
 * Copyright (c) 2012 - 2023 Arvato Systems GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import api.media.*


/**
 * Package which contains the definitions of all essential message elements.
 */
package (com.arvatosystems) t9t.cfg.be XML PROPERTY {
    default private unsigned trim noControlChars;
    properties cmdline;

    type namedQualifier         is Ascii(20);

    /** Brands for relational databases (accessed via JPA). */
    enum DatabaseBrandType {
        H2              = "H2",
        POSTGRES        = "PSQL",
        ORACLE          = "ORA",
        MS_SQL_SERVER   = "MSSQL",
        HANA            = "HANA"
//        MYSQL           = "MYSQL"     // not yet supported
    }

    class T9tServerConfiguration xmlRoot {
        optional Unicode(36)                        serverIdSelf;                    // the server ID of this cluster (see key in uplink configuration)
        optional Ascii(20)                          stagingType;                     // distinguishes between dev, prod and various test environments. See enum StagingType for allowed strings
        required Ascii(16)                          persistenceUnitName;             // technical username
        optional (RelationalDatabaseConfiguration...) databaseConfiguration;         // references access to the main database (which can be read/only). This is the link used by JPA
        optional (RelationalDatabaseConfiguration...) secondaryDatabaseConfig;       // references access to a database for read/write operations by JDBC. If blank, the main database will be used.
        optional (RelationalDatabaseConfiguration...) shadowDatabaseConfig;          // references a possible separate link to a readonly database, which possibly has some latency because it is a replica of the main
        optional (NoSqlDatabaseConfiguration...)    noSqlConfiguration;
        optional (KeyPrefetchConfiguration...)      keyPrefetchConfiguration;
        optional (LogWriterConfiguration...)        logWriterConfiguration;
        optional (ApplicationConfiguration...)      applicationConfiguration;
        optional (ServerConfiguration...)           serverConfiguration;
        optional (Bpm2Configuration...)             bpm2Configuration;
        optional (AWSConfiguration...)              awsConfiguration;
        optional (AzureConfiguration...)            azureConfiguration;
        required (UplinkConfiguration...) List<>    uplinkConfiguration;
        optional (SearchConfiguration...)           searchConfiguration;
        optional (KafkaConfiguration...)            kafkaConfiguration;
        optional (AsyncTransmitterConfiguration...) asyncMsgConfiguration;
        optional (LdapConfiguration...)             ldapConfiguration;              // optional external authentication provider (LDAP)
        optional (OidConfiguration...)              oidConfiguration;               // optional external authentication provider (OpenID Connect, currently only MS-AAD supported)
        optional Uuid                               passwordResetApiKey;            // API key for the mobile UI "password reset" functionality
        optional Uuid                               noDbBackendApiKey;              // API key for servers running without DB access
        required Unicode(255) List<>                noDbBackendPermittedRequests;   // entries for additional request PQONs to be permitted for the above API key
        optional Long                               jwtValidityApiKey;              // validity duration of a JWT when signed on by API key
        optional Long                               jwtValidityUserPassword;        // validity duration of a JWT when signed on by username / password
        optional Long                               jwtValidityOpenId;              // validity duration of a JWT when signed on via MS OpenID Connect
        required Ascii(16) List<>                   mocks;                          // entries for mocked SMTP, defacto etc.
        optional Boolean                            runInCluster;                   // if true, activates sending of cache invalidation events via vert.x event bus
        optional Boolean                            disableScheduler;               // cluster server - if set, do not run jobs here
        optional Boolean                            sessionLogSysout;               // session log to stdout / LOGGER: MUST be set in readonly environments
        optional Ascii(8)                           importEnvironment;              // allows to select a subset of Camel import routes - all will be used if null
        optional Ascii(8)                           eventEnvironment;               // allows to select a subset of event subscriptions - all will be used if null
        optional Ascii(8)                           schedulerEnvironment;           // allows to select a subset of scheduled tasks - all will be used if null
        required Unicode(2000) List<>               z;                              // arbitrary extension parameters, of the form (identifier)=(value)
        optional Uuid                               startupApiKey;                  // API key to be used during startup, if the faked session is not application / sufficient
    }

    class KafkaConfiguration {
        optional Unicode(1024)                      defaultBootstrapServers;        // comma-separated list of bootstrap servers (default for DataSinkDTO.bootstrapServers)
        optional Unicode(1024)                      topicPattern;                   // if not null, subscribe to this pattern instead of individual topics collected via DataSinks (saves resubscribe after DataSinkChanged events)
        optional Uuid                               defaultImportApiKey;            // API-Key for data imports (for example topicPattern related ones)
        optional Integer                            separateWorkerPoolSize;         // specifies the size of a separate input processor thread pool size
        optional Unicode(200)                       clusterManagerGroupId;          // leave null to use the default of "t9tRequestGroup"
        optional Unicode(200)                       clusterManagerTopicName;        // leave null to use the default of "t9tRequestTopic"
        optional Uuid                               clusterManagerApiKey;           // API-Key for request processing on the main topic
        optional Integer                            clusterManagerPoolSize;         // specifies the number of workers for processing requests of the main topic
        optional Boolean                            clusterManagerOrdering;         // use a processor of incoming requests which honors order of requests per partition
        optional Integer                            maxPending;                     // continue polling more requests once at most this number of old requests are still pending (default 3)
        optional Integer                            hardLimitPending;               // hard limit (no timeout) how many tasks can be pending (default 24)
        optional Integer                            slowProcessingTimeout;          // after how much time are we polling more despite (in ms)? (default 15000)
        optional Integer                            waitInterval;                   // after how many ms to evaluate again (default 50)
        optional Boolean                            commitSync;                     // if true, perform synchronous commits (default async)
        optional Integer                            commitInterval;                 // after how many ms to commit offsets (default is 3000)
        optional Integer                            monitorInterval;                // after how many ms to print monitored partitions (default is 5000)
        optional Integer                            shutdownThreadpoolInterval;     // timeout in ms for threadpool shutdown (default is 10000)
        optional Integer                            maxPollRecords;                 // maximum number of records returned in a single call to poll() (max.poll.records)
        optional Integer                            idleInterval;                   // interval in ms to sleep/idle when not busy
    }

    class RelationalDatabaseConfiguration {
        optional Ascii(16)                          persistenceUnitName;              // override default persistenceUnitName from T9tServerConfiguration (e.g. for shadow DB)
        optional Ascii(16)                          username;                         // technical username
        optional Unicode(32)                        password;                         // password
        optional Enum DatabaseBrandType             databaseBrand;                    // which vendor of DB you are using
        optional Unicode(255)                       jdbcDriverClass;                  // class name of JDBC driver
        optional Unicode(255)                       jdbcConnectString;                // connect string for the data source
        optional Boolean                            readonly;                         // set to true, if the provided URL is readonly
        optional Boolean                            readonlyOptimization;             // set session to readonly if request handler indicates that
        required Unicode(255) List<>                migrations;                       // flyway migrations to check, if started with -m option
        required Unicode(2000) List<>               z;                                // arbitrary extension parameters, of the form (identifier)=(value)
        optional Integer                            hikariMaximumPoolSize;            // maximum size that the pool is allowed to reach
        optional Integer                            hikariMinimumIdle;                // minimum number of idle connections
        optional Integer                            hikariMaxLifetime;                // maximum lifetime of a connection
        optional Unicode(255)                       hikariExceptionOverrideClassName; // SQLExceptionOverride class name
        optional Boolean                            fwDropBeforeMigrate;              // Drop all views, triggers and functions as beforeMigrate callback
        optional Boolean                            fwSkipDefaultCallbacks;           // Whether Flyway should skip the default callbacks. If true, only custom callbacks are used.
    }

    class NoSqlDatabaseConfiguration {
        optional namedQualifier                     strategy;                       // select the implementation
        optional Ascii(100)                         hostname;
        optional Integer                            port;
        optional Ascii(30)                          schemaName;
    }

    class KeyPrefetchConfiguration {
        optional namedQualifier                     strategy;                       // select the implementation
        required int                                locationOffset;                 // offset for the location (0 / 5000)
        optional Integer                            defaultKeyChunkSize;            // (currently unused) how many PK values to fetch at a time? (i.e. sequence scaling)
        optional Integer                            prefetchedKeyChunkSize;         // (currently unused) how many PK values to fetch at a time for entities with prefetching?
        optional Integer                            numKeyFetchThreads;             // (currently unused) how many threads are actively fetching keys?
        optional Integer                            cacheSize;                      // common default cache size across all RTTIs for the scaled IDs (scaled = * 10000 + RTTI + locationOffset)
        optional Integer                            cacheSizeUnscaled;              // common default cache size for the unscaled sequence results
        optional Boolean                            useSequencePerTable;            // if null or false: use RTTI for sequences, if true: sequences per table
        optional Long                               sequenceReplicationScale;       // if null, then unscaled refs will be multiplied by this factor (2 if left null)
    }

    class Bpm2Configuration {
        optional Boolean                            jobExecutorEnabled;             // Enable background BPMN job execution on this node? (default=true)
        optional Integer                            jobExecutorQueueSize;           // Job executor queue size (default=3)
        optional Integer                            jobExecutorMinWorker;           // Job executor thread pool min size (default=3)
        optional Integer                            jobExecutorMaxWorker;           // Job executor thread pool max size (default=10)
        optional Ascii(255)                         jobExecutorLockId;              // ID use to lock jobs for this node in DB. MUST BE UNIQUE IN CLUSTER SETUP! (default=random UUID)
    }

    class AWSConfiguration {
        optional Unicode(255)                       region;
        optional Ascii(255)                         snsEndpoint;
        optional Ascii(255)                         sqsEndpoint;
        optional Ascii(255)                         dynamoEndpoint;
        optional Unicode(255)                       dynamoSecret;
    }

    class AzureConfiguration {
        optional Unicode(255)                       region;
        optional Unicode(64)                        userAgent;
        optional Ascii(255)                         cosmosEndpoint;
        optional Unicode(255)                       cosmosSecret;
    }

    class LogWriterConfiguration {
        optional namedQualifier                     strategy;                       // select the implementation
        optional Integer                            queueSize;                      // how many records can be written before the process blocks? (only for asynchronous)
        optional Integer                            alertOnQueueSize;               // how many records should be in the queue to trigger an alert?
        optional Integer                            alertInterval;                  // since measuring queue size takes time, it is only done every n writes
        optional Long                               maxWriteTimeInMillis;           // how long may the DB take to write the records?
        optional Ascii(64)                          kafkaTopic;                     // in case logs are written to a kafka topic: the topic name
        optional Ascii(4)                           serverType;                     // the functional type of server / service (null for main) (written into the message log)
    }

    class ServerConfiguration {
        optional Integer                            port                        properties cmdline="p;port"; // on which port to listen
        optional Unicode(80)                        filesystemContext;              // the base of the file system under $HOME, only used if pathRoot is not set, defaults to "fortytwo"
        optional Unicode(255)                       filesystemPathRoot;             // the base of the file system (defaults to $HOME/fortytwo)
        optional Boolean                            jcaBridge;                      // defines if this instance should send events to a wildfly server
        optional Boolean                            runScheduler;                   // define if the quartz scheduler is supposed to be run on this server.
        optional Unicode(255)                       keyStorePath;                   // if set (recommended for production use!), overrides the standard keystore file (specifies file system path)
        optional Unicode(63)                        keyStorePassword;               // if set (recommended for production use!), overrides the standard keystore password
        optional Unicode(64)                        timeZone;                       // a time zone identifier such as "Europe/Berlin", to define the default time zone of the application
        optional Ascii(64)                          forbiddenCharacters;            // characters to reject when encountered in Strings
        optional Char                               replacementCharacter;           // character to use as a replacement (requests are rejected if none has been defined)
        optional Boolean                            enablePlugins;                  // allow plugins to be uploaded (must be true in order to allow LoadPluginRequest)
        optional Boolean                            enableGenericCallouts;          // allow callouts to other instances for arbitrary requests (must be true in order to allow ExecuteRemoteRequest)
        optional Boolean                            sharedFilesystem;               // all servers, gateways and the ZK UI share the same file system (allows lazy loading of media data)
    }

    class ApplicationConfiguration {
        optional Integer                            workerPoolSize              properties cmdline=";workerPoolSize";              // vert.x DEFAULT_WORKER_POOL_SIZE [vert.x-worker-thread-(n)]
        optional Integer                            eventLoopPoolSize           properties cmdline=";eventLoopPoolSize";           // vert.x DEFAULT_EVENT_LOOP_POOL_SIZE  [vert.x-eventloop-thread-(n)]
        optional Integer                            maxWorkerExecuteTime        properties cmdline=";maxWorkerExecuteTime";        // vert.x DEFAULT_MAX_WORKER_EXECUTE_TIME, in seconds
        optional Integer                            maxEventLoopExecuteTime     properties cmdline=";maxEventLoopExecuteTime";     // vert.x DEFAULT_MAX_EVENT_LOOP_EXECUTE_TIME in seconds
        optional Integer                            blockedThreadCheckInterval  properties cmdline=";blockedThreadCheckInterval";  // vert.x DEFAULT_BLOCKED_THREAD_CHECK_INTERVAL in seconds
        optional Integer                            autonomousPoolSize;             // Executor pool size for autonomous transactions. Defaults to 4 [t9t-autonomous-(n)]
        optional Integer                            localAsyncPoolSize;             // Executor pool size for async requests - for local servers (non vert.x based) it defaults to 4, for vert.x based servers if not configured, the standard worker pool will be used
        optional Integer                            idempotencyCacheMaxEntries;     // How many entries to allow per tenant for the idempotency cache (null means deactivate cache)
        optional Integer                            idempotencyCacheExpiry;         // How many seconds before expiry in the idempotency cache (null means no expiry)
        optional Integer                            numberOfRetriesOptimisticLock;  // how often to retry in case of an optimistic lock exception (default 2)
        optional Integer                            numberOfRetriesDatabaseConnect; // how often to retry in case of database connection exceptions (default 3)
        optional Integer                            pauseBeforeDatabaseConnectRetry; // how many ms to sleep before a retry is done
        optional Double                             pauseIncreaseFactor;            // if set, provides the factor for subsequent sleeps (should be >= 1.0, default 1.5)
        optional Integer                            dnsTtl;                         // time to live for DNS resolution (required for some AWS deployments for Aurora failovers)
        optional Boolean                            useShadowDatabaseForReports;            // use the shadow DB for queries issued from jasper reports
        optional Boolean                            useShadowDatabaseForMonitoringQueries;  // use the shadow DB for queries on huge tables such as log transactions and async messages
        optional Boolean                            useShadowDatabaseForCombinedTextSearch; // use the shadow DB for (possibly partially) SOLR based searches
        required Ascii(16) List<>                   useShadowDatabaseForModule;             // list of modules of downstream projects which should use the shadow database
    }

    /** The uplink configuration has been reassigned to provide the link to the report server. */
    class UplinkConfiguration {
        required Unicode(36)                        key;                            // the key to use for this configuration
        required Unicode(255)                       url;                            // URL for uplink. If not provided, uplink calls will throw an Exception
        optional Unicode(255)                       identityServiceUrl;             // URL for authentication. Request and refresh access tokens.
        optional Enum MediaType                     mediaType;                      // which format to use (JSON, compact, bonaparte, default is compact bonaparte, entry currently ignored)
        optional Uuid                               apiKey;                         // the API key to use
        optional Integer                            timeoutInMs;                    // if specified, use the timeout given here for connect, otherwise some hardcoded default
        optional Unicode(255)                       callbackUrl;                    // the configuration for callback, in case required
        optional Unicode(255)                       basicAuth;                      // the http BASIC auth to use (in case API keys are not supported by the remote)
        optional Unicode(255)                       clientId;                       // Id used to request access token
        optional Unicode(255)                       extraParam;                     // one additional extra parameter, for example HMAC
        optional Unicode(2048)                      token;                          // access token
        optional Integer                            requestTimeoutInMs;             // if specified, use this timeout for maximum request duration, otherwise use the connect timeout, otherwise some hardcoded default
        optional Boolean                            failOnUnknownProperties;        // (only for JACKSON JSON APIs): if TRUE, don't ignore unknown properties, but throw an exception (useful for testing)
        optional Boolean                            internalService;                // true only if the configuration is for internal server (not 3rd party or external service)
    }

    class SearchConfiguration {
        required namedQualifier                     strategy;                       // select the implementation (SOLR, Elastic etc...)
        required Unicode(255)                       defaultUrl;                     // default URL, for example of a SOLR server. Providers can override by DB
    }

    class AsyncTransmitterConfiguration {
        required namedQualifier                     strategy;                       // select the implementation. "LTQ", "KAFKA", "NOOP" are available in core
        required int                                maxMessageAtStartup;            // how many persisted messages to load initially at max [100]
        required int                                timeoutIdleGreen;               // sleep time in ms when queue is empty and last request was green [500 ms]
        required int                                timeoutIdleRed;                 // sleep time in ms after an error [5000 ms]
        required int                                timeoutShutdown;                // sleep time in ms while waiting for graceful shutdown [1000 ms]
        required int                                timeoutExternal;                // max allowable duration of an external request [1000 ms]
        required int                                waitAfterExtError;              // sleep time in ms after an initial remote problem [10000 ms]
        required int                                waitAfterDbErrors;              // sleep time in ms after a database error [60000 ms]
        optional Boolean                            doNotPersistMessages;           // for queue-based messages: no not persist message in DB (not recommended)
        optional Boolean                            processQueues;                  // if not set, queues may be written do, but will not be sent out by this server type
    }

    /** Configuration for LDAP server or other external authentication service. */
    class LdapConfiguration {
        required Unicode(255)                       contextFactory;     // for LDAP, use com.sun.jndi.ldap.LdapCtxFactory
        required Unicode(255)                       providerUrl;        // URL of external server (ldap://server:port) where port = 389 for plain, 636 for SSL
        required Unicode(255)                       securityPrincipal;  // admin / dn user ("uid=admin,ou=special users,o=xx.com")
        required Unicode(63)                        securityCredentials;// admin pw
        required Unicode(255)                       organization;       // o=xx.com
        optional Boolean                            onlySelectedUsers;  // if set to TRUE, then only selected users will use LDAP / external authentication (reads user master data before)
    }

    /** Configuration for OpenID Connect / OAuth2 (via MS AAD). */
    class OidConfiguration {
        required t9t.base.types.defaultExternalId   audience;           // UUID of the application (client ID, "aud" field)
        required t9t.base.types.defaultExternalId   tenantId;           // UUID of MS AD tenant within which the application has been created (for "iss" field)
        optional Ascii(80)                          issuer;             // full string for issuer (defaults to "https://sts.windows.net/${tenantId}/")
        optional Ascii(80)                          jwksUri;            // URL to obtain key
        required boolean                            mustMatchOid;       // if a user has a configured userIdExt, it must match the received "oid" field
        required boolean                            mustMatchIdp;       // if a user has a configured identityProvider entry, it must match the received nvl("idp", "iss") field
        required boolean                            updateOid;          // if a user authenticates via upn, and oid is made available in JWT, update UserDTO with it
        required boolean                            updateIdp;          // if a user authenticates via upn, and idp/iss is made available in JWT, update UserDTO with it
        optional Boolean                            updateName;         // if a user authenticates, update the name field from JWT into UserDTO
        optional Boolean                            updateEmail;        // if a user authenticates, update the email field from JWT into UserDTO
    }
}
